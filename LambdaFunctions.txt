/*------------------------------------------------------------------------------------------------------------------
      Name: AppendTwoColumns
      Comment: 
------------------------------------------------------------------------------------------------------------------/*
=LAMBDA(range1,range2,
  LET(
          RowsRng1, ROWS(range1),
          RowsTotal, ROWS(range1) + ROWS(range2),
          Seq,SEQUENCE(RowsTotal),
          IF(
              Seq<=RowsRng1, INDEX(range1, Seq),
              INDEX(range2, Seq-RowsRng1)
             )
         )
)


/*------------------------------------------------------------------------------------------------------------------
      Name: StackArray
      Comment: This is a comment
------------------------------------------------------------------------------------------------------------------/*
=LAMBDA(ArrayToStack,
     LET(
           ColCount, COLUMNS(ArrayToStack),
           RowCount, ROWS(ArrayToStack),
           CellCount, ColCount * RowCount,
           Seq, SEQUENCE(CellCount),
           RowIndex, MOD(Seq-1,RowCount)+1,
           ColIndex, ROUNDUP(Seq/RowCount, 0),
           ErrorValue, 1/0,
           ReturnValue, INDEX(ArrayToStack, RowIndex, ColIndex),
           ArrayInclErrors, IF(ReturnValue = "", ErrorValue, ReturnValue),
           FILTER(ArrayInclErrors, NOT(ISERROR(ArrayInclErrors)))
        )
)


/*------------------------------------------------------------------------------------------------------------------
      Name: SplitText
      Comment: https://exceljet.net/formula/lambda-split-text-to-array
------------------------------------------------------------------------------------------------------------------/*
=LAMBDA(text,delim,
      TRANSPOSE(
           FILTERXML(
                "<x><y>" & SUBSTITUTE(text,delim,"</y><y>") & "</y></x>",
                 "//y"
             )
       )
)


